plugins {
    id "java"
    id "application"
    id "org.beryx.runtime" version "1.8.0"
    id "org.openjfx.javafxplugin" version "0.0.8"
}

group "blaise.img2latex"
version "0.4.3"

compileJava {
    sourceCompatibility = 11
    targetCompatibility = 11
}

repositories {
    mavenCentral()
}

javafx {
    version = "11.0.2"
    modules = ["javafx.controls", "javafx.swing"]
}

dependencies {
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "org.openjfx:javafx-graphics:11.0.2:mac"
    implementation "org.openjfx:javafx-graphics:11.0.2:win"
    implementation "org.openjfx:javafx-graphics:11.0.2:linux"
    implementation "org.scilab.forge:jlatexmath:1.0.7"
    implementation "org.apache.commons:commons-lang3:3.9"
    implementation "org.apache.httpcomponents:httpcore:4.4.12"
    implementation "org.apache.httpcomponents:httpclient:4.5.10"
}

application {
    mainClassName = "entry.Main"
    applicationName = "Image2LaTeX"
}

task("addInformation") {
    doLast {
        file("src/main/resources/project.properties").write("applicationName=$applicationName\nversion=$version\n")
    }
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    manifest {
        attributes "Main-Class": "entry.Main"
    }
}

jar.dependsOn(addInformation)

runtime {
    options = ["--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages"]
    imageZip = file("$buildDir/libs/Image2LaTeX-" + version + ".zip")
    targetPlatform("linux", "jdk/linux-jdk")
    targetPlatform("windows", "jdk/windows-jdk")
    targetPlatform("macos", "jdk/osx-jdk.jdk/Contents/Home")
}
